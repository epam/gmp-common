/*
 *  /***************************************************************************
 *  Copyright (c) 2017, EPAM SYSTEMS INC
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *     http://www.apache.org/licenses/LICENSE-2.0
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 *  ***************************************************************************
 */

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'io.codearte.nexus-staging' version '0.30.0'
}

group group
version version

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = rootProject.name

repositories {
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                username nexusUsername
                password nexusPassword
            }
        }
    }
    publications {
        sonatypeOss(MavenPublication) {
            pom {
                name = rootProject.name
                description = 'Common module for GroovyManiPulator'
                url = 'https://github.com/epam/gmp-common'
                from components.java
                scm {
                    url = 'https://github.com/epam/gmp-common'
                    connection = 'scm:https://github.com/epam/gmp-common.git'
                    developerConnection = 'scm:https://github.com/epam/gmp-common.git'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'asamuta'
                        name = 'Andrei Samuta'
                        email = 'a.samuta@gmail.com'
                    }
                }
            }
        }
    }
}

dependencies {
    api "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    api "org.slf4j:slf4j-api:${slf4jVersion}"
    api "org.freemarker:freemarker:${freemarkerVersion}"
    api "org.apache.httpcomponents:httpclient:${httpcomponentsVersion}"
    testImplementation "junit:junit:${junitVersion}"
    testImplementation "org.slf4j:slf4j-simple:${slf4jVersion}"
}

jar {
    manifest {
        attributes("Implementation-Title": "${rootProject.group}:${rootProject.name}",
                "Implementation-Version": archiveVersion,
                "Implementation-Vendor": "EPAM SYSTEMS INC")
    }
}

if (project.hasProperty('signing.keyId')) {
    signing {
        sign configurations.archives
        sign publishing.publications.sonatypeOss
    }
}

nexusStaging {
    packageGroup = project.getGroup()
    numberOfRetries = 40
    delayBetweenRetriesInMillis = 10000
}