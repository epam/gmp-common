/*
 *  /***************************************************************************
 *  Copyright (c) 2017, EPAM SYSTEMS INC
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *     http://www.apache.org/licenses/LICENSE-2.0
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 *  ***************************************************************************
 */

plugins {
    id 'io.codearte.nexus-staging' version '0.11.0'
}

apply plugin: 'maven'
apply plugin: 'java'
apply plugin: 'groovy'

apply plugin: 'signing'

group group
version version

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = rootProject.name

repositories {
    mavenCentral()
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: nexusUsername, password: nexusPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: nexusUsername, password: nexusPassword)
            }

            pom.project {
                packaging 'jar'
                name 'gmp-common'
                description 'Common module for GroovyManiPulator'
                url 'https://github.com/epam/gmp-common'

                scm {
                    url 'https://github.com/epam/gmp-common'
                    connection 'scm:https://github.com/epam/gmp-common.git'
                    developerConnection 'scm:https://github.com/epam/gmp-common.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'asamuta'
                        name 'Andrei Samuta'
                        email 'a.samuta@gmail.com'
                    }
                }
            }
        }
    }
}

dependencies {
    testCompile "junit:junit:${junitVersion}"
    compile "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    compile "org.slf4j:slf4j-api:${slf4jVersion}"
    compile "org.freemarker:freemarker:${freemarkerVersion}"
    compile "org.apache.httpcomponents:httpclient:${httpcomponentsVersion}"
}

jar {
    manifest {
        attributes("Implementation-Title": "${rootProject.group}:${rootProject.name}",
                "Implementation-Version": version,
                "Implementation-Vendor": "EPAM SYSTEMS INC")
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

nexusStaging {
    packageGroup = project.getGroup()
}